{IndentAction, languages} = import 'vscode'
{rx} = import '@fink/std-lib/regex.fnk'

wordPattern = rx'
    (-?\d*\.\d\w*)
    |
    ([^~!@$^&*()=+\-\[{\]}\\|:\'",.<>/\s]+)
  '


jsx_conf = dict:
  wordPattern,
  onEnterRules: list:
    dict:
      beforeText: rx'^.*[^/]>$'
      afterText: rx'^$'
      action: dict:
        indentAction: IndentAction.Indent

    dict:
      beforeText: rx'^.*<.+$'
      afterText: rx'^.*$'
      action: dict:
        indentAction: IndentAction.Indent



fink_conf = dict:
  wordPattern,
  onEnterRules: list:
    # match doc comment
    dict:
      beforeText: rx'^\s*---\s*$'
      action: dict:
        indentAction: IndentAction.None

    # match end of str on its own: '|
    dict:
      beforeText: rx'^\s*[\'"]{1}\s*$'
      afterText: rx'^$',
      action: dict:
        indentAction: IndentAction.Outdent

    # match first indentation after auto closing: '|'
    dict:
      beforeText: rx'^.*[\'"]{1}\s*$'
      afterText: rx'^[\'"]$'
      action: dict:
        indentAction: IndentAction.Indent

    # match first indentating existing: foo = '| spam...
    dict:
      beforeText: rx'^.+[=,:(]\s*[\'"]{1}\s*$'
      action: dict:
        indentAction: IndentAction.Indent

    # match operators and blocks: fold ...:|
    dict:
      beforeText: rx'^.+[:=+<\-/*]{1}\s*$'
      action: dict:
        indentAction: IndentAction.Indent


init_lang = fn:
  languages.setLanguageConfiguration 'fink-jsx', jsx_conf
  languages.setLanguageConfiguration 'fink', fink_conf

