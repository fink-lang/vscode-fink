{workspace, languages, Diagnostic, DiagnosticSeverity} = import 'vscode'
{length, is_empty} = import '@fink/std-lib/iter.fnk'
{lower_case, decode} = import '@fink/std-lib/str.fnk'
{new} = import '@fink/js-interop/reflect.fnk'

{range} = import './loc.fnk'
{get_refs, get_decls, decls_by_scope, get_idents, ref_loc} = import './traverse.fnk'



get_unrefferenced = fn refs:
  pipe refs:
    filter {decl}:
      match decl:
        {parent: {parent: {expr: {type: 'module'}}}}: false
        else: true

    filter {idents}:
      match idents:
        1 == length ?: true
        else:
          false



get_undefined = fn scopes, idents:
  pipe idents:
    filter {expr}:
       match expr:
         {type: 'jsx:elem', name: lower_case expr.name}: false
         else: true

    filter node:
      decls = get_decls node, scopes
      match decls:
        is_empty ?: true
        else: false



get_errors = fn ast:
  idents = get_idents ast
  scopes = decls_by_scope idents
  refs = get_refs idents, scopes

  unrefed = pipe refs:
    get_unrefferenced
    map {decl}: ['unused', decl]

  undef = pipe idents:
    get_undefined scopes, ?
    map node: ['undefined', node]

  [...unrefed, ...undef]



diagnostic = fn loc, msg, severity:
  rng = range loc
  new Diagnostic, rng, msg, severity



process_doc = fn doc, dc, parse:
  {errors, ...ast} = await parse doc

  diags = seq:
    ...pipe get_errors ast:
      map [kind, {expr}]:
        loc = ref_loc expr
        diagnostic loc, kind, DiagnosticSeverity.Error

    ...pipe errors:
      map {error, token: {loc}}:
        diagnostic loc, error, DiagnosticSeverity.Error

  dc.set doc.uri, diags
  [diags, errors, ast.exprs]



handle_doc_change = fn doc, dc, parse:
  match doc:
    {languageId: 'fink'}: process_doc doc, dc, parse
    else: false



fake_doc = fn uri: rec:
  uri
  version: 0
  getText: fn:
    data = await workspace.fs.readFile uri
    decode data, 'utf-8'



lint_workspace = fn diags, async_parse:
  files = await workspace.findFiles '**/*.fnk', '**/node_modules/**'

  pipe files:
    map uri:
      doc = fake_doc uri
      process_doc doc, diags, async_parse
    [...?]



add_linter = fn async_parse:
  diags = languages.createDiagnosticCollection 'fink'

  workspace.onDidChangeTextDocument
    fn {document}: handle_doc_change document, diags, async_parse

  workspace.onDidOpenTextDocument
    fn document: handle_doc_change document, diags, async_parse

  lint_workspace diags, async_parse


