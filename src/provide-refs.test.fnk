{describe, it, expect, mock, mock_fn} = import '@fink/jest/test.fnk'
{to_equal} = import '@fink/jest/test.fnk'

{parse} = import '@fink/larix/parser.fnk'


mock
  'vscode'
  fn: rec:
    Position: mock_fn fn ...pos: {pos}
    Range: mock_fn fn ...range: {range}
    Location: mock_fn fn ...location: {location}

    languages: rec:
      registerReferenceProvider: mock_fn _

  {virtual: true}


{languages} = import 'vscode'

{add_refs_provider} = import './provide-refs.fnk'


async_parse = fn doc:
  await parse
    doc.getText _
    '${doc.uri}'

ctx = rec:
  subscriptions: rec:
    push: mock_fn _

document = rec:
  getText: fn: '
    foo = 123
    bar = foo.bar + 456
    spam = fn foo:
      log foo, bar

    fn:
      foo + 1

    Foo = fn: <>{foo}</>

    <Foo ni={foo}>
      {foo}
    </Foo>
    undef
    '
  uri: {test: 'test-uri:test.fnk'}
  languageId: 'fink'



describe 'reference provider', fn:

  it 'provides refs for ident', fn:
    add_refs_provider ctx, async_parse
    {mock: {calls: [[, provider]]}} = languages.registerReferenceProvider

    pos = {line: 0, character: 2}

    expect
      await provider.provideReferences document, pos
      to_equal list:
        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [0, 0]}, {pos: [0, 3]}]}

        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [1, 6]}, {pos: [1, 9]}]}

        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [6, 2]}, {pos: [6, 5]}]}

        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [8, 13]}, {pos: [8, 16]}]}

        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [10, 9]}, {pos: [10, 12]}]}

        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [11, 3]}, {pos: [11, 6]}]}


  it 'provides refs for jsx elem', fn:
    add_refs_provider ctx, async_parse
    {mock: {calls: [[, provider]]}} = languages.registerReferenceProvider

    pos = {line: 8, character: 2}

    expect
      await provider.provideReferences document, pos
      to_equal list:
        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [8, 0]}, {pos: [8, 3]}]}

        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [10, 1]}, {pos: [10, 4]}]}


  it 'provides refs for shadowed idents', fn:
    add_refs_provider ctx, async_parse
    {mock: {calls: [[, provider]]}} = languages.registerReferenceProvider

    pos = {line: 3, character: 7}

    expect
      await provider.provideReferences document, pos
      to_equal list:
        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [2, 10]}, {pos: [2, 13]}]}

        rec: location: list:
          {test: 'test-uri:test.fnk'}
          {range: [{pos: [3, 6]}, {pos: [3, 9]}]}


  it 'provides no refs for unbound idents', fn:
    add_refs_provider ctx, async_parse
    {mock: {calls: [[, provider]]}} = languages.registerReferenceProvider

    pos = {line: 13, character: 1}

    expect
      await provider.provideReferences document, pos
      to_equal []

