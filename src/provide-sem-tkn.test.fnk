{describe, it, expect, mock, mock_fn} = import '@fink/jest/test.fnk'
{to_equal, was_called_with} = import '@fink/jest/test.fnk'

{parse} = import '@fink/larix/parser.fnk'


mock_builder = rec:
  push: mock_fn _
  build: mock_fn fn: 'mock-build'


mock
  'vscode'
  fn: rec:
    SemanticTokensLegend: mock_fn fn ...args: {mock_legend: args}
    SemanticTokensBuilder: mock_fn fn: mock_builder

    languages: rec:
      registerDocumentSemanticTokensProvider: mock_fn _


    workspace: rec:
      _onDidChangeTextDocument: mock_fn _ # TOOD: needed?

  {virtual: true}



{languages, SemanticTokensBuilder} = import 'vscode'

{add_sem_tokens_provider} = import './provide-sem-tkn.fnk'


async_parse = fn doc:
  await parse
    doc.getText _
    '${doc.uri}'


describe 'sem token provider', fn:

  it 'provides tokens', fn:
    ctx = rec:
      subscriptions: rec:
        push: mock_fn _

    add_sem_tokens_provider ctx, async_parse
    {mock: {calls: [[, provider]]}} = languages.registerDocumentSemanticTokensProvider


    tokens = await provider.provideDocumentSemanticTokens rec:
      getText: fn: "
        foo bar.spam shrub
        bar = 'spam \${ham egg} ni'
        ni = foo.(bar) shrub

        foo = (shrub + ni) * 42

        pipe foo:
          map fn item:
            {items=123} = match item:
              [..., ni]: {na: []}

            ...items
        "

    expect
      SemanticTokensBuilder
      was_called_with {mock_legend: [['function', 'string', 'variable'], []]}

    expect
      mock_builder.push.mock.calls
      to_equal list:
        [0, 0, 3, 0]
        [0, 8, 4, 0]
        [1, 14, 3, 0]
        [2, 9, 1, 0]
        [2, 13, 1, 0]
        [7, 2, 3, 0]
        [9, 18, 2, 2, 0]

    expect
      tokens
      to_equal 'mock-build'


  it 'handles errors', fn:
    ctx = rec:
      subscriptions: rec:
        push: mock_fn _

    add_sem_tokens_provider ctx, async_parse
    {mock: {calls: [[, provider]]}} = languages.registerDocumentSemanticTokensProvider


    tokens = await provider.provideDocumentSemanticTokens rec:
      uri: {toString: fn: './test.fnk'}
      getText: fn: '
          foo bar
          ['

    expect
      SemanticTokensBuilder
      was_called_with {mock_legend: [['function', 'string', 'variable'], []]}

    expect
      mock_builder.push.mock.calls
      to_equal list:
        [0, 0, 3, 0]

    expect
      tokens
      to_equal 'mock-build'



