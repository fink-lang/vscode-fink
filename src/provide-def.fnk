{languages} = import 'vscode'

{get_decls, decls_by_scope, get_idents} = import './traverse'
{is_at_loc} = import './traverse'
{to_larix_pos, doc_loc} = import './loc'


find_decls_by_loc = fn ast, loc:
  idents = get_idents ast
  scopes = decls_by_scope idents

  pipe idents:
    filter {expr}: is_at_loc expr, loc

    map ident:
      [decl] = get_decls ident, scopes
      decl.expr



def_provider = fn async_parse: fn doc, pos:
  [ast] = await async_parse doc

  [...locs] = pipe find_decls_by_loc ast, to_larix_pos pos:
    map decl:
      doc_loc doc, decl.loc.start

  locs


add_definition_provider = fn {subscriptions}, async_parse:

  subscr = languages.registerDefinitionProvider
    'fink'
    {provideDefinition: def_provider async_parse }

  subscriptions.push subscr
