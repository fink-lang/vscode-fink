{languages, SemanticTokensLegend, SemanticTokensBuilder} = import 'vscode'
{for_each, length} = import '@fink/std-lib/iter'
{new} = import '@fink/js-interop/reflect'

{walk} = import './traverse'


token_types = ['function', 'string', 'variable']
token_modifiers = []
sem_token_legend = new SemanticTokensLegend, token_types, token_modifiers

get_callee = fn node:
  match node:
    {type: 'ident'}:
      node
    {type: 'member'}:
      get_callee node.right
    else:
      node


add_call_tokens = fn expr, builder:
  callee = get_callee expr.callee
  match callee:
    {type: 'ident'}:
      {value, loc: {start}} = callee
      ident_len = length value
      builder.push start.line - 1, start.column, ident_len, 0

    {type: 'group'}:
      {loc: {start, end}} = callee
      builder.push start.line - 1, start.column, 1, 0
      builder.push end.line - 1, end.column - 1, 1, 0


add_prop_tokens = fn prop, builder:
  match prop:
    {left: {type: 'ident'}, right: {type: 'assign'}}:
      false

    {left: {type: 'ident'}, right: ? != prop.left}:
      {value, loc: {start}} = prop.left
      key_len = length value
      builder.push start.line - 1, start.column, key_len, 2, 0


get_tokens = fn {errors, ...ast}:
  builder = new SemanticTokensBuilder, sem_token_legend

  pipe walk ast:
    for_each fn {expr}:
      match expr:
        {type: 'call'}: add_call_tokens expr, builder
        {type: 'object:prop'}: add_prop_tokens expr, builder

  list:
    builder.build ()
    errors


provide_sem_tokens = fn async_parse: fn doc:
  parsed = await async_parse doc
  [tokens] = get_tokens parsed
  tokens


add_sem_tokens_provider = fn {subscriptions}, async_parse:

  subscr = languages.registerDocumentSemanticTokensProvider
    'fink'
    {provideDocumentSemanticTokens: provide_sem_tokens async_parse}
    sem_token_legend

  subscriptions.push subscr
