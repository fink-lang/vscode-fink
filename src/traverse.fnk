{parse} = import '@fink/larix'


concat = fn ...iterables:
  pipe iterables:
    map iterable:
      ...iterable


walk_exprs = fn exprs, props:
  pipe exprs:
    map expr:
      ...walk expr, props


walk = fn expr, props={scope: {expr}}:
  node = {expr, ...props}
  parent = node

  concat
    [node]
    match expr:
      {type: 'module'}:
        walk_exprs expr.exprs, {parent, decl: false, scope: node}

      {type: 'block', op: 'match'}:
        concat
          walk_exprs expr.args, {parent, decl: false, scope: node.scope}
          walk_exprs expr.exprs, {parent, decl: false, scope: node}

      {type: 'block', op: 'pipe'}:
        concat
          walk_exprs expr.args, {parent, decl: false, scope: node.scope}
          walk_exprs expr.exprs, {parent, decl: false, scope: node}

      {type: 'block', args: []}:
        concat
          walk_exprs expr.args, {parent, decl: true, scope: node}
          walk_exprs expr.exprs, {parent, decl: false, scope: node}

      {type: 'block'}:
        walk_exprs expr.exprs, {parent, decl: false, scope: node}

      {type: 'call'}:
        concat
          walk expr.callee, {parent, decl: false, scope: node.scope}
          walk_exprs expr.args, {parent, decl: false, scope: node.scope}

      {type: 'object'}:
        walk_exprs expr.exprs, {parent, decl: props.decl, scope: node.scope}

      {type: 'object:prop'}:
        concat
          walk expr.left, {parent, decl: false, scope: node.scope}
          walk expr.right, {parent, decl: props.decl, scope: node.scope}

      {type: 'member'}:
        concat
          walk expr.left, {parent, decl: false, scope: node.scope}
          walk expr.right, {parent, decl: false, scope: node.scope}

      {type: 'assign'}:
        concat
          walk expr.left, {parent, decl: true, scope: node.scope}
          walk expr.right, {parent, decl: false, scope: node.scope}

      {type: 'string'}:
        walk_exprs expr.exprs, {parent, decl: false, scope: node.scope}

      {type: 'array'}:
        walk_exprs expr.exprs, {parent, decl: props.decl, scope: node.scope}

      {type: 'group'}:
        walk_exprs expr.exprs, {parent, decl: props.decl, scope: node.scope}

      {left: {}, right: {}}:
        concat
          walk expr.left, {parent, decl: false, scope: node.scope}
          walk expr.right, {parent, decl: false, scope: node.scope}

      {right: {}}:
        walk expr.right, {parent, decl: props.decl, scope: node.scope}

      else:
        []
